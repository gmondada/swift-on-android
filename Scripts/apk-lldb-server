#!/bin/zsh

# assumptions:
# - main activity is .MainActivity

set -e


# parse arguments

declare -a OPTS=()
while (( "$#" )); do
    case "$1" in
        -*) OPTS+=("$1") ; shift ;;
        *)  APP_ID="$1"  ; shift ;;
    esac
done
APP_ROOT=/data/data/$APP_ID


# check the environment

if [ ! -d "$ANDROID_NDK_HOME" ]; then
	echo "Error: ANDROID_NDK_HOME is not set properly."
	exit
fi

if ! adb --version > /dev/null; then
	echo "Error: adb command is no available."
	exit
fi

if [ "$APP_ID" = "" ]; then
	echo "Error: Application ID is missing. Please provide it as an argument."
	exit
fi

# clean up anything left behind from earlier debugging sessions

function remove_port_forwarding() {
	TMP=$(adb forward --list | grep "$1" || true)
	if [ "$TMP" = "" ]; then return; fi
	while IFS= read -r LINE; do
		PORT=$(echo $LINE | awk '{print $2}')
		adb forward --remove $PORT
	done <<< "$TMP"
}

adb shell am force-stop $APP_ID
adb shell run-as $APP_ID killall lldb-server 2> /dev/null || true
remove_port_forwarding "localabstract.*$APP_ID"
remove_port_forwarding "localabstract.*gdbserver"
adb shell  "run-as $APP_ID find $APP_ROOT -name 'gdbserver.*' -exec rm {} \;"


# install lldb-server in the app sandbox

TARGET_ARCH=$(adb shell uname -m)
#LLDB_SERVER="/Applications/Android Studio.app/Contents/plugins/android-ndk/resources/lldb/android/arm64-v8a/lldb-server"
LLDB_SERVER="$(echo $ANDROID_NDK_HOME/toolchains/llvm/prebuilt/darwin-*/lib/clang/*/lib/linux/$TARGET_ARCH/lldb-server)"
if [ ! -f "$LLDB_SERVER" ]; then
	echo "Error: lldb-server not found for target architecture: $TARGET_ARCH"
	exit
fi
adb shell mkdir -p /data/local/tmp/lldb-stuff
adb push "$LLDB_SERVER" /data/local/tmp/lldb-stuff
adb shell run-as $APP_ID mkdir -p $APP_ROOT/lldb-stuff
adb shell "cat /data/local/tmp/lldb-stuff/lldb-server | run-as $APP_ID sh -c 'cat > $APP_ROOT/lldb-stuff/lldb-server'"
adb shell run-as $APP_ID chmod 700 $APP_ROOT/lldb-stuff/lldb-server


# start the app

adb shell am start -n $APP_ID/$APP_ID.MainActivity -a android.intent.action.MAIN -c android.intent.category.LAUNCHER $OPTS


# wait for the app to be alive and its PID to be known

APP_PID=""
while [ "$APP_PID" = "" ]; do
	sleep 0.1
	APP_PID=$(adb shell pidof $APP_ID || echo -n "")
done

echo "App ID: $APP_ID"
echo "Process ID: $APP_PID"


# save environment

mkdir -p ~/.lldb/android
cat >~/.lldb/android/last-env <<EOF
APP_ID="$APP_ID"
APP_PID="$APP_PID"
EOF


# save lldb startup commands

cat >~/.lldb/android/last-start-commands << EOF
platform select remote-android
command alias a0 platform connect unix-abstract-connect:///$APP_ID/lldb-platform.sock
command alias a1 process attach --pid $APP_PID
command alias a1n process attach --name $APP_ID
command alias a2 process handle SIGSEGV -n false -p true -s false
command alias a3 process handle SIGBUS -n false -p true -s false
command alias a4 process handle SIGCHLD -n false -p true -s false
a0
a1
a2
a3
a4
EOF


# start lldb-server

echo "Starting lldb-server"
adb shell run-as $APP_ID $APP_ROOT/lldb-stuff/lldb-server platform --server --listen unix-abstract:///$APP_ID/lldb-platform.sock --log-channels "lldb process:gdb-remote packets"
#adb shell /data/local/tmp/lldb-stuff/lldb-server platform --server --listen unix-abstract:///$APP_ID-0/lldb-platform.sock --log-channels "lldb process:gdb-remote packets"
